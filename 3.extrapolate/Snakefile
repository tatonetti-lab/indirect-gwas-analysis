"""Show that Indirect GWAS can be used to extrapolate to new traits.

Suppose we have a set of phenotypes (called feature traits) with existing GWAS
summary statistics. If a new phenotype can be approximated as a linear
combination of the feature traits, then we can approximate the GWAS for the new
phenotype using Indirect GWAS. This analysis shows that real human phenotypes
can be well-approximated using linear combinations, and that, accordingly,
approximated GWAS (via Indirect GWAS) can be highly accurate.

This functionality is useful for extrapolating GWAS to new phenotype
definitions, beyond the phenotypes for which GWAS have been conducted.
"""

import numpy as np
import pandas as pd
import polars as pl


rule all:
  input:
    # Inputs
    "../data/pheno/binary_pheno.tsv",
    "../data/pheno/covar.tsv",
    # Outputs
    "data/phecode_map/phecode_definitions1.2.csv",
    "data/phecode_map/Phecode_map_v1_2_icd10_beta.csv",
    "data/pheno/phecodes.tsv",
    "data/coef/binary_to_phecode.tsv",
    "data/pcov/adjusted_covariance.tsv",


rule download_phecode_map:
  output:
    def_df = "data/phecode_map/phecode_definitions1.2.csv",
    map_df = "data/phecode_map/Phecode_map_v1_2_icd10_beta.csv",
  shell:
    """
    wget -O {output.def_df}.zip https://phewascatalog.org/files/phecode_definitions1.2.csv.zip
    unzip {output.def_df}.zip -d data/phecode_map

    wget -O {output.map_df}.zip https://phewascatalog.org/files/Phecode_map_v1_2_icd10_beta.csv.zip
    unzip {output.map_df}.zip -d data/phecode_map
    """


rule assign_phecodes:
  input:
    def_df = "data/phecode_map/phecode_definitions1.2.csv",
    map_df = "data/phecode_map/Phecode_map_v1_2_icd10_beta.csv",
    phenos = "../data/pheno/binary_pheno.tsv",
  output:
    "data/pheno/phecodes.tsv"
  conda:
    "phd"
  notebook:
    "1.assign_phecodes.py.ipynb"


rule regress_phecodes:
  input:
    phecode = "data/pheno/phecodes.tsv",
    binary = "../data/pheno/binary_pheno.tsv",
  output:
    "data/coef/binary_to_phecode.tsv"
  conda:
    "phd"
  notebook:
    "2.regress_phecodes.py.ipynb"


rule gwas:
  input:
    geno = multiext("../data/geno/geno_500k", ".pgen", ".psam", ".pvar"),
    pheno = "data/pheno/phecodes.tsv",
    covar = "../data/pheno/covar.tsv",
  output:
    "data/gwas/plink.log",
  params:
    geno_prefix = "../data/geno/geno_500k",
    output_prefix = "data/gwas/plink",
  threads: 35
  benchmark: "benchmarks/gwas.txt"
  shell:
    """
    plink2 \
      --pfile {params.geno_prefix} \
      --glm zs hide-covar \
      --pheno {input.pheno} \
      --covar {input.covar} \
      --threads {threads} \
      --out {params.output_prefix}
    """


rule adjusted_covariance:
  input:
    pheno = "../data/pheno/binary_pheno.tsv",
    covar = "../data/pheno/covar.tsv",
  output:
    "data/pcov/adjusted_covariance.tsv",
  run:
    pheno_df = pl.read_csv(input.pheno, separator="\t")
    covar_df = pl.read_csv(input.covar, separator="\t")
    merged_df = pheno_df.join(covar_df, on=["FID", "IID"])
    Y = merged_df.select(pheno_df.drop(["FID", "IID"]).columns).to_numpy()
    X = merged_df.select(covar_df.drop(["FID", "IID"]).columns).to_numpy()
    beta = np.linalg.lstsq(X, Y, rcond=None)[0]
    residuals = Y - X @ beta
    cov = np.cov(residuals, rowvar=False)
    pheno_names = pheno_df.drop(["FID", "IID"]).columns
    names = [f"plink.{name}.glm.linear.zst" for name in pheno_names]
    (
        pd.DataFrame(cov, index=names, columns=names)
        .to_csv(output[0], sep="\t")
    )
